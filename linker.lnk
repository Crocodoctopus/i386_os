OUTPUT_FORMAT(binary)
OUTPUT_ARCH(i386)
SECTIONS {
	STAGE2_SRC = LOADADDR(.stage2)/512 + 1;
	STAGE2_DST = ADDR(.stage2);
	STAGE2_SIZE = (SIZEOF(.stage2) + 511)/512;

	KERNEL_SRC = LOADADDR(.kernel_start)/512;
	KERNEL_DST = 0x400000;
	KERNEL_LEN = (.kernel_end - ADDR(.kernel_start) + 511)/512;

	PAGE_DIRECTORY_PHYS = KERNEL_DST;

	kernel_start = ADDR(.kernel_start);
	kernel_stack = ADDR(.kernel_stack);
	
	.stage1 0x7C00 : AT(0x0) {
		tmp/stage1.o (.stage1)
	}
	.bootsig : AT(0x1FE) {
		tmp/stage1.o (.bootsig)
	}

	.stage2 0x8000 : AT(0x200) {
		tmp/stage2.o
		tmp/stage2c.o
	}
	
	.kernel_start 0xF0000000 : AT(0x800) {
		tmp/paging.o (.paging_dir)		
	}
	.kernel : {
		* (*)
	}
	.kernel_stack : ALIGN(0x4) {
		. = . + 0x1000;
	}
	.kernel_end = .;
}
